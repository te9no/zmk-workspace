default:
    @just --list --unsorted

build := absolute_path('.build')
west_workspace := absolute_path('.west-workspace')
zmk_config_root := absolute_path(`
  if [ -f .west-workspace/.west/config ]; then
    west_top=".west-workspace"
    west_config="$west_top/.west/config"
  elif [ -f .west/config ]; then
    west_top="."
    west_config=".west/config"
  else
    echo "."
    exit 0
  fi

  if [ -n "${west_config:-}" ]; then
    path=$(awk -F ' *= *' '/^ *path/ {print $2}' "$west_config")
    file=$(awk -F ' *= *' '/^ *file/ {print $2}' "$west_config")
    west_yml_path="$west_top/${path:-.}/${file}"
    echo "$(dirname $west_yml_path)/.."
  fi
`)
zmk_config_name := `
  if [ -f .west-workspace/.west/config ]; then
    west_top=".west-workspace"
    west_config="$west_top/.west/config"
  elif [ -f .west/config ]; then
    west_top="."
    west_config=".west/config"
  else
    echo "default"
    exit 0
  fi

  path=$(awk -F ' *= *' '/^ *path/ {print $2}' "$west_config")
  file=$(awk -F ' *= *' '/^ *file/ {print $2}' "$west_config")
  west_yml_path="$west_top/${path:-.}/${file}"
  basename "$(realpath -m "$(dirname "$west_yml_path")/..")"
`
out := absolute_path('firmware') / zmk_config_name

# run a just recipe in the build container
_container *args:
    #!/usr/bin/env bash
    set -euo pipefail
    exec "{{ justfile_directory() }}/just.sh" {{ args }}

# parse build.yaml and filter targets by expression
_parse_targets $expr:
    #!/usr/bin/env bash
    set -euo pipefail
    if [[ "${IN_ZMK_CONTAINER:-0}" != "1" ]]; then
        exec just _container _parse_targets "{{ expr }}"
    fi

    attrs="[.board, .shield, .snippet, .\"artifact-name\"]"
    filter="(($attrs | map(. // [.]) | combinations), ((.include // {})[] | $attrs)) | join(\",\")"
    echo "$(yq -r "$filter" "{{ zmk_config_root }}/build.yaml" | grep -v "^," | grep -i "${expr/#all/.*}")"

# build firmware for single board & shield combination
_build_single $board $shield $snippet $artifact *west_args:
    #!/usr/bin/env bash
    set -euo pipefail
    artifact="${artifact:-${shield:+${shield// /+}-}${board}}"
    echo "::zmk-build-start::${artifact}"

    # Board ids may contain '/' (e.g. xiao_ble//zmk). Slashes break cp paths and mkdir.
    artifact_fs="${artifact//\//-}"
    build_dir="{{ build / '$artifact_fs' }}"
    echo "Building firmware for $artifact..."

    # Check if zephyr/module.yml exists to determine whether to include DZMK_EXTRA_MODULES
    if [[ -f "{{ zmk_config_root }}/zephyr/module.yml" ]]; then
        (
            cd "{{ west_workspace }}"
            west build -p auto -s zmk/app -d "$build_dir" -b $board {{ west_args }} ${snippet:+-S "$snippet"} -- \
                -DZephyr_DIR="{{ west_workspace }}/zephyr/share/zephyr-package/cmake" \
                -DZMK_CONFIG=""{{ zmk_config_root }}/config"" -DZMK_EXTRA_MODULES="{{ zmk_config_root }}" ${shield:+-DSHIELD="$shield"}
        )
    else
        (
            cd "{{ west_workspace }}"
            west build -p auto -s zmk/app -d "$build_dir" -b $board {{ west_args }} ${snippet:+-S "$snippet"} -- \
                -DZephyr_DIR="{{ west_workspace }}/zephyr/share/zephyr-package/cmake" \
                -DZMK_CONFIG=""{{ zmk_config_root }}/config"" ${shield:+-DSHIELD="$shield"}
        )
    fi

    if [[ -f "$build_dir/zephyr/zmk.uf2" ]]; then
        mkdir -p "{{ out }}" && cp "$build_dir/zephyr/zmk.uf2" "{{ out }}/$artifact_fs.uf2"
    else
        mkdir -p "{{ out }}" && cp "$build_dir/zephyr/zmk.bin" "{{ out }}/$artifact_fs.bin"
    fi
    echo "::zmk-build-done::${artifact}"

# build firmware for matching targets
build expr *west_args:
    #!/usr/bin/env bash
    set -euo pipefail
    if [[ "${IN_ZMK_CONTAINER:-0}" != "1" ]]; then
        exec just _container build "{{ expr }}" {{ west_args }}
    fi

    targets="$(just _parse_targets {{ expr }})"
    [[ -z "$targets" ]] && echo "No matching targets found. Aborting..." >&2 && exit 1

    while IFS=, read -r board shield snippet artifact; do
        [[ -z "${board:-}" ]] && continue
        just _build_single "$board" "$shield" "$snippet" "$artifact" {{ west_args }}
    done <<< "$targets"

# clear build cache and artifacts
clean:
    rm -rf {{ build }} {{ out }}

# show ccache statistics
ccache-stats *args:
    #!/usr/bin/env bash
    set -euo pipefail
    if [[ "${IN_ZMK_CONTAINER:-0}" != "1" ]]; then
        exec just _container ccache-stats {{ args }}
    fi

    ccache -s {{ args }}

# clear ccache data
clean-ccache:
    #!/usr/bin/env bash
    set -euo pipefail
    if [[ "${IN_ZMK_CONTAINER:-0}" != "1" ]]; then
        exec just _container clean-ccache
    fi

    ccache -C

# clear all automatically generated files
clean-all: clean
    #!/usr/bin/env bash
    set -euo pipefail

    generated=()
    if [[ -d .west ]]; then
        while IFS= read -r path; do
            case "$path" in
                "$(pwd)/config"| "$(pwd)/config/"*) ;;
                "$(pwd)"/*) generated+=("$path") ;;
            esac
        done < <(WEST_TOPDIR="$(pwd)" west list -f '{abspath}' 2>/dev/null || true)
    fi

    rm -rf .west "{{ west_workspace }}" zmk zephyr modules "${generated[@]}"

# clear nix cache
clean-nix:
    nix-collect-garbage --delete-old

# initialize west
init *config_path:
    #!/usr/bin/env bash
    set -euo pipefail
    if [[ "${IN_ZMK_CONTAINER:-0}" != "1" ]]; then
        exec just _container init {{ config_path }}
    fi

    config_path="{{ config_path }}"

    # If config_path is provided as argument, use fzf to select it
    if [[ -z "$config_path" ]]; then
        # Use fzf to select config from config/ and its subdirectories
        subdirs=$(find config -mindepth 1 -maxdepth 1 -type d -exec basename {} \; | sort)
        candidates=$(printf "config\n"; printf "%s\n" "$subdirs" | sed 's#^#config/#')

        config_path=$(echo "$candidates" | fzf \
            --prompt="Select ZMK config: " \
            --header="Choose a configuration to initialize" \
            --color="fg:#000000,bg:#ffffff,fg+:#000000,bg+:#d9d9d9,hl:#005f87,hl+:#005f87,info:#444444,prompt:#005f87,pointer:#005f87,marker:#005f87,spinner:#005f87,header:#444444" \
            --preview="ls -1a {}")

        if [[ -z "$config_path" ]]; then
            echo "No config selected. Exiting..."
            exit 0
        fi
    fi

    # Determine west.yml path
    if [[ -f "$config_path/west.yml" ]]; then
        west_yml_abs="$config_path/west.yml"
    else
        west_yml_abs="$config_path/config/west.yml"
    fi

    # Convert to path relative to config
    west_yml_rel=$(realpath --relative-to=config "$west_yml_abs")

    mkdir -p "{{ west_workspace }}/.west"
    printf '[manifest]\npath = ../config\nfile = %s\n' "$west_yml_rel" > "{{ west_workspace }}/.west/config"

    (
        cd "{{ west_workspace }}"
        west update --fetch-opt=--filter=blob:none
        west zephyr-export
    )

# list build targets
list:
    #!/usr/bin/env bash
    set -euo pipefail
    if [[ "${IN_ZMK_CONTAINER:-0}" != "1" ]]; then
        exec just _container list
    fi
    just _parse_targets all | sed 's/,*$//' | sort | column

# update west
update:
    #!/usr/bin/env bash
    set -euo pipefail
    if [[ "${IN_ZMK_CONTAINER:-0}" != "1" ]]; then
        exec just _container update
    fi
    (
        cd "{{ west_workspace }}"
        west update --fetch-opt=--filter=blob:none
    )

# draw keymap SVGs with keymap-drawer
draw-keymap *names:
    #!/usr/bin/env bash
    set -euo pipefail
    if [[ "${IN_ZMK_CONTAINER:-0}" != "1" ]]; then
        exec just _container draw-keymap {{ names }}
    fi

    config_root="{{ zmk_config_root }}"
    keymap_dir="$config_root/keymap-drawer"
    keymap_config="$keymap_dir/config.yaml"
    mkdir -p "$keymap_dir"

    keymap_config_args=()
    if [[ -f "$keymap_config" ]]; then
        keymap_config_args=(-c "$keymap_config")
    fi

    requested=({{ names }})
    if [[ ${#requested[@]} -eq 0 ]]; then
        mapfile -t requested < <(find "$config_root/config" -maxdepth 1 -type f -name "*.keymap" -printf "%f\n" | sed "s/\\.keymap$//" | sort)
    fi

    if [[ ${#requested[@]} -eq 0 ]]; then
        echo "No keymap files found in $config_root/config" >&2
        exit 1
    fi

    for name in "${requested[@]}"; do
        keymap_file="$config_root/config/$name.keymap"
        json_file="$config_root/config/$name.json"
        yaml_file="$keymap_dir/$name.yaml"
        svg_file="$keymap_dir/$name.svg"

        if [[ ! -f "$keymap_file" ]]; then
            echo "Keymap file not found: $keymap_file" >&2
            exit 1
        fi

        echo "Drawing keymap for $name..."
        just generate-keymap-json "$name"
        keymap "${keymap_config_args[@]}" parse -z "$keymap_file" -o "$yaml_file"

        draw_args=()
        if [[ -f "$json_file" ]]; then
            draw_args=(-j "$json_file")
        fi
        keymap "${keymap_config_args[@]}" draw "$yaml_file" "${draw_args[@]}" -o "$svg_file"
        echo "Wrote $svg_file"
    done

# generate keymap-drawer JSON from ZMK physical layouts
generate-keymap-json *names:
    #!/usr/bin/env bash
    set -euo pipefail
    if [[ "${IN_ZMK_CONTAINER:-0}" != "1" ]]; then
        exec just _container generate-keymap-json {{ names }}
    fi

    config_root="{{ zmk_config_root }}"
    requested=({{ names }})
    if [[ ${#requested[@]} -eq 0 ]]; then
        mapfile -t requested < <(find "$config_root/config" -maxdepth 1 -type f -name "*.keymap" -printf "%f\n" | sed "s/\\.keymap$//" | sort)
    fi

    if [[ ${#requested[@]} -eq 0 ]]; then
        echo "No keymap files found in $config_root/config" >&2
        exit 1
    fi

    for name in "${requested[@]}"; do
        dtsi_file=$(find "$config_root" -path "$config_root/.git" -prune -o -type f \( -name "$name.dtsi" -o -name "$name.overlay" \) -print | sort | head -n 1)
        if [[ -z "$dtsi_file" ]]; then
            echo "Physical layout source not found for $name under $config_root" >&2
            exit 1
        fi

        json_file="$config_root/config/$name.json"
        layout_name="layout_$name"
        echo "Generating $json_file from $dtsi_file..."
        "{{ justfile_directory() }}/scripts/generate_keymap_drawer_json.py" \
            "$dtsi_file" "$json_file" \
            --layout "$layout_name" \
            --id "$name" \
            --name "$name"
    done

# draw keymap SVGs directly from ZMK physical layouts and .keymap files
draw-physical-layout *names:
    #!/usr/bin/env bash
    set -euo pipefail
    if [[ "${IN_ZMK_CONTAINER:-0}" != "1" ]]; then
        exec just _container draw-physical-layout {{ names }}
    fi

    config_root="{{ zmk_config_root }}"
    layout_dir="$config_root/keymap-svg"
    mkdir -p "$layout_dir"

    requested=({{ names }})
    if [[ ${#requested[@]} -eq 0 ]]; then
        mapfile -t requested < <(find "$config_root/config" -maxdepth 1 -type f -name "*.keymap" -printf "%f\n" | sed "s/\\.keymap$//" | sort)
    fi

    if [[ ${#requested[@]} -eq 0 ]]; then
        echo "No keymap files found in $config_root/config" >&2
        exit 1
    fi

    for name in "${requested[@]}"; do
        keymap_file="$config_root/config/$name.keymap"
        dtsi_file=$(find "$config_root" -path "$config_root/.git" -prune -o -type f \( -name "$name.dtsi" -o -name "$name.overlay" \) -print | sort | head -n 1)
        if [[ ! -f "$keymap_file" ]]; then
            echo "Keymap file not found: $keymap_file" >&2
            exit 1
        fi
        if [[ -z "$dtsi_file" ]]; then
            echo "Physical layout source not found for $name under $config_root" >&2
            exit 1
        fi

        svg_file="$layout_dir/$name.svg"
        layout_name="layout_$name"
        echo "Drawing keymap-svg keymap for $name..."
        "{{ justfile_directory() }}/scripts/generate_physical_layout_svg.py" \
            "$dtsi_file" "$svg_file" \
            --keymap "$keymap_file" \
            --layout "$layout_name"
        echo "Wrote $svg_file"
    done

# upgrade zephyr-sdk and python dependencies
upgrade-sdk:
    nix flake update --flake .

# flash firmware for matching targets
flash expr *args:
    #!/usr/bin/env bash
    set -euo pipefail

    # Check if -r option is provided
    rebuild=false
    build_args=()
    for arg in {{ args }}; do
        if [[ "$arg" == "-r" ]]; then
            rebuild=true
        else
            build_args+=("$arg")
        fi
    done

    # Rebuild if -r option was provided
    if [[ "$rebuild" == "true" ]]; then
        echo "Rebuilding before flashing..."
        just build "{{ expr }}" "${build_args[@]}"
    fi

    target=$(just _parse_targets {{ expr }} | head -n 1)

    if [[ -z "$target" ]]; then
        echo "No matching targets found for expression '{{ expr }}'. Aborting..." >&2
        exit 1
    fi

    IFS=, read -r board shield snippet artifact <<< "$target"
    # Use artifact-name if specified, otherwise construct from shield and board
    if [[ -n "$artifact" ]]; then
        artifact_name="$artifact"
    else
        artifact_name="${shield:+${shield// /+}-}${board}"
    fi
    artifact_fs="${artifact_name//\//-}"
    uf2_file="$artifact_fs.uf2"
    uf2_path="{{ out }}/$uf2_file"

    if [[ ! -f "$uf2_path" ]]; then
        echo "Firmware file '$uf2_path' not found. Please build it first with 'just build \"{{ expr }}\"'." >&2
        exit 1
    fi

    # macOS
    if [[ "$OSTYPE" == "darwin"* ]]; then
        echo "Flashing '$uf2_path'..."
        if [[ -n "${FLASH_TARGET_MOUNT:-}" ]]; then
            ./flash.sh "$uf2_path" "${FLASH_TARGET_MOUNT}"
        else
            ./flash.sh "$uf2_path"
        fi
    # WSL
    elif grep -q -i "Microsoft" /proc/version; then
        echo "Flashing '$uf2_path'..."
        if [[ -n "${FLASH_TARGET_DRIVE:-}" ]]; then
            powershell.exe -ExecutionPolicy Bypass -File flash.ps1 -Uf2File "$(wslpath -w $uf2_path)" -DriveLetter "${FLASH_TARGET_DRIVE}"
        else
            powershell.exe -ExecutionPolicy Bypass -File flash.ps1 -Uf2File "$(wslpath -w $uf2_path)"
        fi
    # Other: Not supported
    else
        echo "Flashing '$uf2_path' is not supported on this platform." >&2
        exit 1
    fi

[no-cd]
test $testpath *FLAGS:
    #!/usr/bin/env bash
    set -euo pipefail
    if [[ "${IN_ZMK_CONTAINER:-0}" != "1" ]]; then
        exec just _container test "{{ testpath }}" {{ FLAGS }}
    fi

    testcase=$(basename "$testpath")
    build_dir="{{ build / "tests" / '$testcase' }}"
    config_dir="{{ '$(pwd)' / '$testpath' }}"
    cd {{ justfile_directory() }}

    if [[ "{{ FLAGS }}" != *"--no-build"* ]]; then
        echo "Running $testcase..."
        rm -rf "$build_dir"
        (
            cd "{{ west_workspace }}"
            west build -s zmk/app -d "$build_dir" -b native_posix_64 -- \
                -DZephyr_DIR="{{ west_workspace }}/zephyr/share/zephyr-package/cmake" \
                -DCONFIG_ASSERT=y -DZMK_CONFIG="$config_dir" \
                ${ZMK_EXTRA_MODULES:+-DZMK_EXTRA_MODULES="$(realpath ${ZMK_EXTRA_MODULES})"}
        )
    fi

    ${build_dir}/zephyr/zmk.exe | sed -e "s/.*> //" |
        tee ${build_dir}/keycode_events.full.log |
        sed -n -f ${config_dir}/events.patterns > ${build_dir}/keycode_events.log
    if [[ "{{ FLAGS }}" == *"--verbose"* ]]; then
        cat ${build_dir}/keycode_events.log
    fi

    if [[ "{{ FLAGS }}" == *"--auto-accept"* ]]; then
        cp ${build_dir}/keycode_events.log ${config_dir}/keycode_events.snapshot
    fi
    diff -auZ ${config_dir}/keycode_events.snapshot ${build_dir}/keycode_events.log
